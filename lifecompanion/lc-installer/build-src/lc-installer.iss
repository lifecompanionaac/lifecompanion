; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; PARAMETERS
; AppVersion = installer version number
; InputDir = installer input dir

; TODO : APP INFORMATIONS
#define AppName "LifeCompanion"
#define AppPublisher "Centre de Kerpape"
#define AppURL "https://lifecompanionaac.org"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{842428C6-5769-4842-98CF-E784B68CA9C5}}
AppName={#AppName}
AppVersion={#AppVersion}
AppVerName=LifeCompanion
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={tmp}
DisableDirPage=yes
DisableProgramGroupPage=yes
OutputBaseFilename="{#AppName}-setup-{#AppVersion}"
Compression=lzma
SolidCompression=yes
WizardStyle=modern
CreateUninstallRegKey=no
Uninstallable=no
DisableWelcomePage=yes
DisableFinishedPage=yes
LanguageDetectionMethod=uilanguage
ShowLanguageDialog=auto
SetupIconFile=lifecompanion_icon.ico
ArchitecturesAllowed=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Files]
Source: "{#InputDir}\**"; DestDir: "{app}" ; Flags: recursesubdirs

[Run]
Filename: "{app}\bin\javaw"; Parameters: "-Djava.net.useSystemProxies=true -m org.lifecompanion.installer/org.lifecompanion.installer.InstallerApp {srcexe} {code:DetectSilent}"; WorkingDir: "{app}\bin"; Flags: runascurrentuser waituntilterminated hidewizard

[Code]

function DetectSilent(Param: String): String;
var
  i: Integer;
begin
  Result := '';
  for i := 1 to ParamCount do
    if (CompareText(ParamStr(i), '/verysilent') = 0) or (CompareText(ParamStr(i), '/silent') = 0) then
    begin
      Result := '/silent';
      Exit;
    end;
end;