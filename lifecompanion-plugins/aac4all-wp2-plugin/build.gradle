plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '8.3.0'
}

javafx {
    version = '22'
    modules = ['javafx.controls', 'javafx.media']
    configuration = 'compileOnly'
}


repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

version = "0.9.1"

dependencies {
    compileOnly "org.lifecompanion:lc-app:$lifecompanionAppVersion"
    implementation 'com.github.gazeplay:TobiiStreamEngineForJava:5.0'
    // If you need to add plugin dependencies, add them with "implementation"
    // Note that you should check that the dependency is not already integrated in LifeCompanion core (if so, use "compileOnly" instead)
}

// Enter your plugin meta data
// When changing ID : think also about changing it everywhere in your code
jar {
    duplicatesStrategy DuplicatesStrategy.WARN
    manifest {
        attributes(
                "LifeCompanion-Plugin-Class": "org.lifecompanion.plugin.aac4all.wp2.AAC4AllWp2Plugin",
                "LifeCompanion-Plugin-Package-Scanning-Base": "org.lifecompanion.plugin.aac4all.wp2",
                "LifeCompanion-Plugin-Id": "aac4all-wp2-plugin",
                "LifeCompanion-Plugin-Author": "Lisa Hoiry, IRIT",
                "LifeCompanion-Plugin-Version": project.version,
                "LifeCompanion-Plugin-Build-Date": new Date().getTime(),
                "LifeCompanion-Plugin-Name": "Étude SK4ALL",
                "LifeCompanion-Plugin-Description": "Plugin de l'étude SKY4ALL dans le cadre du projet de recherche AAC4ALL.",
                "LifeCompanion-Min-App-Version": "$lifecompanionAppVersion"
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
