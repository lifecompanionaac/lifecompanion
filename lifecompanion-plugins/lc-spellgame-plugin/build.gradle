plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

javafx {
    version = '16'
    modules = ['javafx.controls', 'javafx.media']
    configuration = 'compileOnly'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

version = "0.0.1"

dependencies {
    compileOnly "org.lifecompanion:lc-app:$lifecompanionAppVersion"
    // If you need to add plugin dependencies, add them with "implementation"
    // Note that you should check that the dependency is not already integrated in LifeCompanion core (if so, use "compileOnly" instead)
}

// Enter your plugin meta data
// When changing ID : think also about changing it everywhere in your code
jar {
    duplicatesStrategy DuplicatesStrategy.WARN
    manifest {
        attributes(
                "LifeCompanion-Plugin-Class": "org.lifecompanion.plugin.spellgame.SpellGamePlugin",
                "LifeCompanion-Plugin-Package-Scanning-Base": "org.lifecompanion.plugin.spellgame",
                "LifeCompanion-Plugin-Id": "lc-spellgame-plugin",
                "LifeCompanion-Plugin-Author": "LifeCompanion AAC",
                "LifeCompanion-Plugin-Version": project.version,
                "LifeCompanion-Plugin-Build-Date": new Date().getTime(),
                "LifeCompanion-Plugin-Name": "Jeu \"Ecris le mot\"",
                "LifeCompanion-Plugin-Description": "Ajoute à LifeCompanion la possibilité de créer un jeu simple favorisant l'apprentissage de l'orthographe de nouveaux mots.",
                "LifeCompanion-Min-App-Version": "$lifecompanionAppVersion"
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
